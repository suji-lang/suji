# std:random module - random number generation and list utilities
import __builtins__:random_random as random
import __builtins__:random_seed as seed

export {
    random: random,
    seed: seed,

    integer: |a, b| {
        lo = a::ceil()
        hi = b::floor()

        n = hi - lo
        i = (random() * n)::floor()

        lo + i
    },

    pick: |xs| {
        xs[
            (random() * xs::length())::floor()
        ]
    },

    shuffle: |xs| {
        length = xs::length()
        swap = |a, b| {
            tmp = xs[a]
            xs[a] = xs[b]
            xs[b] = tmp
        }

        loop through 0..length with i {
            j = (random() * length)::floor()
            swap(i, j)
        }

        xs
    },

    sample: |xs, k| {
        length = xs::length()
        match {
            length <= k => return xs,
        }

        indexes = []
        loop {
            i = (random() * length)::floor()

            match {
                indexes::length() == k => break,
                indexes::contains(i) => continue,
                _ => indexes::push(i),
            }
        }

        indexes
            ::map(|i| xs[i])
    }
}
