# std:random module - random number generation and list utilities
import __builtins__:random_random as random
import __builtins__:random_seed as seed

pick = |xs| {
    xs[
        (random() * xs::length())::floor()
    ]
}

string = |allowed_chars, length| {
    (0..length)
        ::map(|n| allowed_chars |> pick)
        ::join("")
}

export {
    random: random,
    seed: seed,
    pick: pick,
    string: string,

    integer: |a, b| {
        lo = a::ceil()
        hi = b::floor()

        n = hi - lo
        i = (random() * n)::floor()

        lo + i
    },

    shuffle: |xs| {
        length = xs::length()
        swap = |a, b| {
            tmp = xs[a]
            xs[a] = xs[b]
            xs[b] = tmp
        }

        loop through 0..length with i {
            j = (random() * length)::floor()
            swap(i, j)
        }

        xs
    },

    sample: |xs, k| {
        length = xs::length()
        match {
            length <= k => return xs,
        }

        indexes = []
        loop {
            i = (random() * length)::floor()

            match {
                indexes::length() == k => break,
                indexes::contains(i) => continue,
                _ => indexes::push(i),
            }
        }

        indexes
            ::map(|i| xs[i])
    },

    hex_string: |length = 16| {
        string("0123456789abcdef", length)
    },

    alpha_string: |length = 16, capitals = true| {
        chars = match capitals {
            true => "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz",
            false => "abcdefghijklmnopqrstuvwxyz",
        }

        string(chars, length)
    },

    numeric_string: |length = 16| {
        string("0123456789", length)
    },

    alphanumeric_string: |length = 16, capitals = true| {
        chars = match capitals {
            true => "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789",
            false => "abcdefghijklmnopqrstuvwxyz0123456789",
        }
        
        string(chars, length)
    }
}
